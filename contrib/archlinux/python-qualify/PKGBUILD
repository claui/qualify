# Maintainer: Claudia Pellegrino <clau@tiqua.de>

pkgname=python-qualify
pkgver=0.1.0
pkgrel=1
pkgdesc='Import top-level modules from a directory not on `sys.path`'
arch=('any')
url='https://github.com/claui/python-qualify'
license=('Apache-2.0')
depends=(
  'python-wrapt'
)
makedepends=(
  'python-build'
  'python-hatchling'
  'python-installer'
  'python-myst-parser'
  'python-sphinx'
  'python-sphinx-autoapi'
  'python-sphinx_rtd_theme'
  'python-wheel'
)
checkdepends=('python-pytest')
options=('!strip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/claui/python-qualify/archive/v${pkgver}.tar.gz")
sha512sums=('00000000')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  echo >&2 'Building wheel'
  python -m build --wheel --no-isolation

  echo >&2 'Generating man page'
  sphinx-build -aqEW -b man doc/sphinx build/man
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python -m pytest
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  echo >&2 'Packaging the wheel'
  python -I -m installer --destdir="${pkgdir}" dist/*.whl

  echo >&2 'Packaging README.md'
  install -D -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    'README.md'

  echo >&2 'Packaging the license'
  install -D -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    'LICENSE'

  echo >&2 'Packaging the man page'
  install -D -m 644 -t "${pkgdir}/usr/share/man/man1" \
    build/man/*.3
}
